Main = init; Modify!
Modify = inc; prepare!; expand!

init () [
	(n1 (R), empty) | 
] => [
	(n1 (R), empty)
	(n2, empty # grey)
	(n3, empty)
	(n4, empty)
	| 
	(e1, n2, n3, empty)
	(e2, n3, n4, empty)
	(e3, n4, n2, empty) 
]
interface = {n1}


inc () [
	(n1 (R), empty) | (e1, n1, n1, empty)
] => [
	(n1 (R), empty) |
]
interface = {n1}

prepare () [
	(n1, empty # grey)
	(n2, empty)
	|
	(e1, n1, n2, empty)
] => [
	(n1, empty # grey)
	(n2, empty )
	|
	(e1, n1, n2, empty # dashed)
]
interface = {n1,n2}

expand () [
	(n1, empty # grey)
	(n2, empty )
	(n3, empty)
	| 
	(e1, n1, n2, empty # dashed)
	(e2, n2, n3, empty) 
	(e3, n3, n1, empty)
] => [
	(n1, empty # grey)
	(n2, empty)
	(n3, empty)

	(n4, empty)
	(n5, empty)
	(n6, empty # grey)
	|
	(e1, n1, n4, empty)
	(e2, n5, n1, empty) 
	(e3, n4, n5, empty)

	(e5, n4, n6, empty) 
	(e4, n2, n4, empty)
	(e7, n5, n6, empty) 
	(e6, n3, n5, empty)

	(e8, n6, n2, empty)
	(e9, n6, n3, empty)
]
interface = {n1, n2, n3}

